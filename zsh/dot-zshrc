# vim: filetype=zsh

# NOTE: Checkout https://peterlyons.com/problog/2018/01/zsh-lazy-loading/
#  and https://notes.frederic-hemberger.de/speeding-up-initial-zsh-startup-with-lazy-loading/
#  for lazy-loading

# NOTE: run with: ZPROF=1 zsh -i -c exit
[ -z "$ZPROF" ] || zmodload zsh/zprof

. "$HOME/.zsh.d/options.zsh"
. "$HOME/.zsh.d/keybinds.zsh"
. "$HOME/.zsh.d/alias.zsh"
. "$HOME/.zsh.d/zellij.zsh"

#######################
# --- Plugins ------- #
#######################

# NOTE: plugin sourcing does not work in other files.

export ZSH_PLUGIN_PATH="$HOME/.local/share/zsh/plugins"

# Defer plugin sourcing
ssource "${ZSH_PLUGIN_PATH}/zsh-defer/zsh-defer.plugin.zsh"

# Use syntax highlighting
zsh-defer ssource "${ZSH_PLUGIN_PATH}/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

. "$HOME/.zsh.d/prompt.zsh" # FIXME: Has to be after syntax-highlighting why?

# Use history substring search
setup_history_search(){
    unfunction "$0"
    ssource "${ZSH_PLUGIN_PATH}/zsh-history-substring-search/zsh-history-substring-search.zsh"
    # bind UP and DOWN arrow keys to history substring search
    bindkey "$terminfo[kcuu1]" history-substring-search-up
    bindkey "$terminfo[kcud1]" history-substring-search-down
    bindkey '^[[A' history-substring-search-up
    bindkey '^[[B' history-substring-search-down
    bindkey '^P' history-substring-search-up
    bindkey '^N' history-substring-search-down
}
zsh-defer setup_history_search

# jump quickly to directories that where visited frequently
if  (( ! $+commands[zoxide] )); then
    zsh-defer ssource "${ZSH_PLUGIN_PATH}/zsh-z/zsh-z.plugin.zsh"
fi
# More suggsetions
fpath+="${ZSH_PLUGIN_PATH}/zsh-completions/src"

# Use autosuggestion
setup_autosuggestion(){
    unfunction "$0"
    ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
    ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=8'
    ssource "${ZSH_PLUGIN_PATH}/zsh-autosuggestions/zsh-autosuggestions.zsh"

    # Bind ctrl-Y to accept the current suggestion.
    bindkey '^Y' autosuggest-accept
    bindkey '^Z' autosuggest-accept # Swiss keyboard
}
zsh-defer setup_autosuggestion

#####################
# -- Completions -- #
#####################

# Add fzf completions and key-bindings to zsh
# sudo apt install fzf
if (( $+commands[fzf] )); then
    zsh-defer source <(fzf --zsh)
fi

# Cargo/Rust
if (( $+commands[rustup] )); then
    if [ ! -e "$ZFUNC_PATH/_rustup" ]; then
        mkdir -p "$ZFUNC_PATH"
        rustup completions zsh > "$ZFUNC_PATH/_rustup"
    fi
    if [ ! -e $ZFUNC_PATH/_cargo ]; then
        mkdir -p "$ZFUNC_PATH"
        rustup completions zsh cargo > "$ZFUNC_PATH/_cargo"
    fi
fi

# Must be after comptinit:
if (( $+commands[zoxide] )); then
    eval "$(zoxide init zsh)"
fi

##############
# -- SLOW -- #
##############

# Nvm
setup_nvm() {
    unfunction "$0"
    export NVM_DIR="$HOME/.config/nvm"
    [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
    [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion (also zsh)
}
zsh-defer setup_nvm

# Pyenv
setup_pyenv() {
    unfunction "$0"
    export PYENV_ROOT="$HOME/.pyenv"
    [[ -d $PYENV_ROOT/bin ]] && export PATH="$PYENV_ROOT/bin:$PATH"
    eval "$(pyenv init - zsh)"
}
zsh-defer setup_pyenv

[ -z "$ZPROF" ] || zprof
